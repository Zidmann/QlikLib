// ----------------------------------------------------------------------------------------------------------------
/*
    Author      : Zidmann (emmanuel.zidel@gmail.com)
    Script      : table.qvs
    Version     : 0.2.0
    Description : Module containing all the table management functions
 */
// ----------------------------------------------------------------------------------------------------------------

LET vErrorCount = $(ScriptErrorCount);

// ----------------------------------------------------------------------------------------------------------------
// FUNCTIONS TO INTERACT WITH THE TABLES
// ----------------------------------------------------------------------------------------------------------------

/*
 * Description : Dropping a table after checking it exists (to avoid any error)
 *       Input : vTableName  - Name of the table to drop
 *      Output : None
 */
Sub table_drop(vTableName)
    call table_exist(vTableName);
    IF (vReturn <> 0) THEN
        DROP TABLE $(vTableName);
    END IF;
End sub;

/*
 * Description : Renaming a table
 *               If the destination table already exists the table is overwritten
 *       Input : vTableNameSrc - Source table
 *               vTableNameDst - Destination table
 *      Output : vReturn - 0 : nothing done since the table doesn't exist
 *                         1 : the table was simply renamed
 *                         2 : the table was renamed after dropping the previous table
 */
Sub table_rename(vTableNameSrc, vTableNameDst)
    call table_exist(vTableNameSrc);
    IF (vReturn = 0) THEN
        vReturn=0;
    ELSE
        call table_exist(vTableNameDst);
        IF (vReturn<>0) THEN
            call table_drop(vTableNameDst);
            vReturn=2;
        ELSE
            vReturn=1;
        END IF;
        RENAME TABLE $(vTableNameSrc) TO $(vTableNameDst);
    END IF;
End sub;

/*
 * Description : Dropping a column in a table after checking both exist (to avoid any error)
 *       Input : vTableName  - Name of the table to watch
 *               vColumnName - Name of the column to drop
 *      Output : None
 */
Sub column_drop(vTableName, vColumnName)
    call column_exist(vTableName, vColumnName);
    IF (vReturn<>0) THEN
        DROP FIELD $(vColumnName) FROM $(vTableName);
    END IF;
End sub;


/*
 * Description : Renaming a column in a table
 *               If the destination column already exists the column is overwritten
 *       Input : vTableName     - Name of the table where to make changes in the columns
 *               vColumnNameSrc - Source column
 *               vColumnNameDst - Destination column
 *      Output : vReturn - 0 : nothing done since the column doesn't exist in this table
 *                         1 : the column was simply renamed
 *                         2 : the column was renamed after dropping the previous column
 */
Sub column_rename(vTableName, vColumnNameSrc, vColumnNameDst)
    call column_exist(vTableName, vColumnNameSrc);
    IF (vReturn = 0) THEN
        vReturn=0;
    ELSE
        call column_exist(vTableName, vColumnNameDst);
        IF (vReturn<>0) THEN
            call column_drop(vTableName, vColumnNameDst);
            vReturn=2;
        ELSE
            vReturn=1;
        END IF;
        RENAME FIELD $(vColumnNameSrc) TO $(vColumnNameDst);
    END IF;
End sub;

/*
 * Description : Write data from a table into a file
 *       Input : vTableName  - Name of the table to write
 *               vFilePath   - Path of the file where to write data
 *               vAppend     - Define if the file is overwritten or if the data are concatenated
 *                             0 : truncate mode (default)
 *                             1 : concatenation mode
 *               vBufferName - Temporary table name
 *      Output : None
 */
Sub table_file_write(vTableName, vFilePath, vAppend, vBufferPath)
    SET vDefaultAppend=0;
    SET vDefaultBufferPath='./tmpBuffer.qvd';

    call variable_fill('vAppend', vDefaultAppend);
    call variable_fill('vBufferPath', vDefaultBufferPath);

    call table_exist(vTableName);
    IF (vReturn<>0) THEN
        IF (vAppend<>0) THEN
            // Backup of the table content in an external file called buffer
            call file_option(vBufferPath);
            STORE $(vTableName) INTO $(vBufferPath) $(vFileWriteOption)
            ;
            call table_drop(vTableName);

            // Load the current content in the file in the table
            call file_option(vFilePath);
            $(vTableName):
            NoConcatenate
            LOAD *
            FROM [$(vFilePath)] $(vFileReadOption)
            ;

            // Add the new content
            call file_option(vBufferPath);
            $(vTableName):
            Concatenate
            LOAD *
            FROM [$(vBufferPath)] $(vFileReadOption)
            ;
        END IF;

        // Writing the final content in the file
        call file_option(vFilePath);
        STORE $(vTableName) INTO $(vFilePath) $(vFileWriteOption)
        ;

        IF (vAppend<>0) THEN
            // Restoring the table as it was
            call table_drop(vTableName);
            call file_option(vBufferPath);
            $(vTableName):
            NoConcatenate
            LOAD *
            FROM [$(vBufferPath)] $(vFileReadOption)
            ;
            
            //Deleting the buffer file - disable because require permissions
//          call file_remove(vBufferPath);
        END IF;
    ELSE
        call console_info('No data to write');
    END IF;

    LET vAppend=vDefaultAppend;
    LET vBufferPath=vDefaultBufferPath;
End  sub;

/*
 * Description : Load data from a file and put them in a table
 *       Input : vFilePath    - Path of the file to read to load data
 *               vTableName   - Name of the table where to write data
 *               vAppend      - Define if the table is overwritten or if the data are concatenated
 *                              0 : truncate mode
 *                              1 : concatenation mode (default)
 *               vPathInclude - Define if the path of the file must be included in the table (column _vFilePath)
 *                              0 : do not print it
 *                              1 : print it
 *      Output : None
 */
Sub file_table_read(vFilePath, vTableName, vAppend, vPathInclude)
    SET vDefaultAppend=1;
    SET vDefaultPathInclude=0;
    call variable_fill('vAppend', vDefaultAppend);
    call variable_fill('vPathInclude', vDefaultPathInclude);

    call file_exist(vFilePath);
    IF (vReturn<>0) THEN
        IF (vAppend=0) THEN
            call table_drop(vTableName);
        END IF;
        call file_option(vFilePath);
        call file_auxi_load(vFilePath, vTableName, vPathInclude);
    ELSE
        call console_info('No file to read');
    END IF;

    LET vAppend=vDefaultAppend;
    LET vPathInclude=vDefaultPathInclude;
End  sub;

/*
 * Description : Load data from one or several files and put them in a table
 *       Input : vFilePatternPath - Pattern of the path of the file(s) to read
 *               vTableName       - Name of the table where to write data
 *               vAppend          - Define if the table is overwritten or if the data are concatenated
 *                                  0 : truncate mode
 *                                  1 : concatenation mode (default)
 *      Output : None
 */
Sub file_table_multiple_read(vFilePatternPath, vTableName, vAppend)
    SET vDefaultAppend=1;
    call variable_fill('vAppend', vDefaultAppend);

    SET vFlag = 0;
    FOR EACH vFilePath in FileList(vFilePatternPath)
        SET vPathInclude = 1;
        IF (vAppend=0 AND vFlag=0) THEN
            call file_table_read(vFilePath, vTableName, vAppend, vPathInclude);
        ELSE
            call file_table_read(vFilePath, vTableName, 1, vPathInclude);
        END IF;
        vFlag=1;
    NEXT vFilePath;

    IF (vFlag=0) THEN
        call console_info('No file to read');
    END IF;

    LET vAppend=vDefaultAppend;
End sub; 

/*
 * Description : Load data from an excel document and put them in a table
 *       Input : vFilePath      - Path of the file to read to load data
 *               vWorksheetName - Name of a worksheet
 *               vTableName     - Name of the table where to write data
 *               vAppend        - Define if the table is overwritten or if the data are concatenated
 *                               0 : truncate mode
 *                               1 : concatenation mode (default)
 *               vPathInclude   - Define if the path of the file must be included in the table (column _vFilePath)
 *                               0 : do not print it
 *                               1 : print it
 *      Output : None
 */
Sub file_excel_sheet(vFilePath, vWorksheetName, vTableName, vAppend, vPathInclude)
    SET vDefaultAppend=1;
    SET vDefaultPathInclude=0;
    call variable_fill('vAppend', vDefaultAppend);
    call variable_fill('vPathInclude', vDefaultPathInclude);

    call excel_worksheet_exist(vFilePath, vWorksheetName);
    IF (vReturn<>0) THEN
        IF (vAppend=0) THEN
            call table_drop(vTableName);
        END IF;

        call file_option(vFilePath, vWorksheetName);
        call file_auxi_load(vFilePath, vTableName, vPathInclude);
    ELSE
        call console_info('No file to read');
    END IF;

    LET vAppend=vDefaultAppend;
    LET vPathInclude=vDefaultPathInclude;
End sub;

Sub file_auxi_load(vFilePath, vTableName, vPathInclude)
    call table_exist(vTableName);
    IF (vReturn <> 0) THEN
        SET vLoadMode = Concatenate;
    ELSE
        SET vLoadMode = NoConcatenate;
    END IF;

    IF (vPathInclude <> 0) THEN
        LET vSelectClause = '''$(vFilePath)'' as [_vFilePath], *';
    ELSE
        SET vSelectClause = *;
    END IF;

    $(vTableName):
    $(vLoadMode)
    LOAD $(vSelectClause)
    FROM ['$(vFilePath)'] $(vFileReadOption)
    ;
End sub;

// ----------------------------------------------------------------------------------------------------------------
// PRINT IF THE LOADING FINISHED WITH OR WITHOUT ANY ERRORS
// ----------------------------------------------------------------------------------------------------------------
LET vDiffErrorCount = $(ScriptErrorCount) - vErrorCount;
IF (vDiffErrorCount > 0) THEN
    call console_error('Table module loaded with $(vDiffErrorCount) error(s)');
ELSE
    call console_info('Table module successfully loaded');
END IF;
